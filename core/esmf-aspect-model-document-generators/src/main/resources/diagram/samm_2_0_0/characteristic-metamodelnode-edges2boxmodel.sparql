# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates edges between Characteristic and Meta Model Properties (dataType)
construct {
  ?edge a :Edge .
  ?edge :title "dataType" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?characteristic samm:dataType ?metaModelElement .
  filter ( ?metaModelElement in ( samm:Property ) )
  bind( func:getElementName( ?characteristic ) as ?characteristicName )
  ?metaModelElement rdfs:label ?metaModelElementName .

  bind( concat( ?characteristicName, "Characteristic" ) as ?characteristicBoxName )
  bind( iri( concat( func:getNamespace( ?characteristic ), ?characteristicBoxName ) ) as ?from )
  bind( concat( ?metaModelElementName, "MetaModelElement" ) as ?metaModelElementBoxName )
  bind( iri( concat( func:getNamespace( ?metaModelElement ), ?metaModelElementBoxName ) ) as ?to )

  bind( iri( concat( func:getNamespace( ?characteristic ), ?characteristicBoxName, "_To_", ?metaModelElementBoxName ) ) as ?edge )
}

