# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix samm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates edges between Quantifiables and Units (unit)
construct {
  ?edge a :Edge .
  ?edge :title "unit" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?quantifiable samm-c:unit ?unit .
  ?property rdf:type samm:Property .
  filter( exists { ?property samm:characteristic/( samm-c:baseCharacteristic* | rdfs:subClassOf* ) ?quantifiable } )
  bind( func:getElementName( ?quantifiable ) as ?quantifiableName )
  bind( replace( str( ?unit ), "[^#]*#(.*)", "$1") as ?unitName )
  bind( concat( ucase( substr( ?unitName, 1, 1 ) ), substr( ?unitName, 2 ) ) as ?upperUnitName )

  bind( concat( ?quantifiableName, "Characteristic" ) as ?quantifiableBoxName )
  bind( iri( concat( func:getNamespace( ?quantifiable ), ?quantifiableBoxName ) ) as ?from )
  bind( concat( ?upperUnitName, "Unit" ) as ?unitBoxName )
  bind( iri( concat( func:getNamespace( ?unit ), ?unitBoxName ) ) as ?to )

  bind( iri( concat( func:getNamespace( ?quantifiable ), ?quantifiableBoxName, "_To_", ?unitBoxName ) ) as ?edge )
}

