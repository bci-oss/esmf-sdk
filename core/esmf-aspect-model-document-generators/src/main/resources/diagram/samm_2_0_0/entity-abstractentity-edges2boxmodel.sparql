# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix samm-e: <urn:samm:org.eclipse.esmf.samm:entity:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates edges between Aspects or Entities or Abstract Entities and Properties
construct {
  ?edge a :Edge .
  ?edge :title "extends" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
    ?extendingEntityOrAbstractEntity samm:extends ?extendedEntityOrAbstractEntity .

    bind( strafter( str( ?extendingEntityOrAbstractEntity ), "#" ) as ?extendingEntityOrAbstractEntityName ) .
    bind( strafter( str( ?extendedEntityOrAbstractEntity ), "#" ) as ?extendedEntityOrAbstractEntityName ) .

    ?extendingEntityOrAbstractEntity rdf:type ?extendingEntityOrAbstractEntityClass .
    ?extendedEntityOrAbstractEntity rdf:type ?extendedEntityOrAbstractEntityClass .

    bind( str(?extendingEntityOrAbstractEntityClass) as ?extendingEntityOrAbstractEntityFullClassName ) .
    bind( strafter(?extendingEntityOrAbstractEntityFullClassName, "#") as ?extendingEntityOrAbstractEntityTypeName ) .

    bind( str(?extendedEntityOrAbstractEntityClass) as ?extendedEntityOrAbstractEntityFullClassName ) .
    bind( strafter(?extendedEntityOrAbstractEntityFullClassName, "#") as ?extendedEntityOrAbstractEntityTypeName ) .

    bind( concat( ?extendingEntityOrAbstractEntityName, ?extendingEntityOrAbstractEntityTypeName ) as ?extendingEntityOrAbstractEntityBoxName ) .
    bind( iri( concat( func:getNamespace( ?extendingEntityOrAbstractEntity ), ?extendingEntityOrAbstractEntityBoxName ) ) as ?from ) .

    bind( concat( ?extendedEntityOrAbstractEntityName, ?extendedEntityOrAbstractEntityTypeName ) as ?extendedEntityOrAbstractEntityBoxName ) .
    bind( iri( concat( func:getNamespace( ?extendedEntityOrAbstractEntity ), ?extendedEntityOrAbstractEntityBoxName ) ) as ?to ) .

    bind( iri( concat( func:getNamespace( ?extendingEntityOrAbstractEntity ), ?extendingEntityOrAbstractEntityBoxName, "_To_", ?extendedEntityOrAbstractEntityBoxName ) ) as ?edge ) .
}