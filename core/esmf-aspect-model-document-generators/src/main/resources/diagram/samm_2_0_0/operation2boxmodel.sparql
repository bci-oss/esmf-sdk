# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates boxes for Operations
construct {
  ?operationBox a :Box .
  ?operationBox :prototype "Operation" .
  ?operationBox :title ?operationName .
  ?operationBox :modelElement ?operation .
  ?operationBox :entries (
     [
       a :Entry ;
       :title "preferredName" ;
       :text ?preferredName
     ]
     [
       a :Entry ;
       :title "description" ;
       :text ?description
     ]
     [
       a :Entry ;
       :title "see" ;
       :text ?seeValues
     ]
  )
} where {
  ?aspect a samm:Aspect .
  ?aspect samm:operations/rdf:rest*/rdf:first ?operation .

  bind( strafter( str( ?operation ), "#" ) as ?operationName ) .

  bind( concat( ?operationName, "Operation" ) as ?boxName )
  bind( iri( concat( func:getNamespace( ?aspect ), ?boxName ) ) as ?operationBox )

  optional {
    ?operation samm:preferredName ?preferredNameValue .
    filter( lang( ?preferredNameValue ) = "en" )
    bind( xsd:string( ?preferredNameValue ) as ?preferredName )
  }

  optional {
    ?operation samm:description ?descriptionValue .
    filter( lang( ?descriptionValue ) = "en" )
    bind( xsd:string( ?descriptionValue ) as ?description )
  }

  optional {
   {
     select ( group_concat( ?seeValue; separator=", " ) as ?seeValues ) ?operation
     where {
       select *
       where {
         ?aspect a samm:Aspect .
         ?aspect samm:operations/rdf:rest*/rdf:first ?operation .
         ?operation samm:see ?seeValue .
       }
       order by str( ?seeValue )
     }
     group by ?operation
   }
  }
}
