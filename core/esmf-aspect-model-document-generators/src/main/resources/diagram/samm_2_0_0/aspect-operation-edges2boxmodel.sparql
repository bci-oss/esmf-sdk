# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generate edges between Aspects and Operations
construct {
  ?edge a :Edge .
  ?edge :title "operation" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?aspect a samm:Aspect .
  bind( strafter( str( ?aspect ), "#" ) as ?aspectName ) .
  ?operation a samm:Operation .
  bind( strafter( str( ?operation ), "#" ) as ?operationName ) .
  filter( exists { ?aspect samm:operations/rdf:rest*/rdf:first ?operation } )

  bind( concat( ?aspectName, "Aspect" ) as ?aspectBoxName )
  bind( iri( concat( func:getNamespace( ?aspect ), ?aspectBoxName ) ) as ?from )
  bind( concat( ?operationName, "Operation" ) as ?operationBoxName )
  bind( iri( concat( func:getNamespace( ?operation ), ?operationBoxName ) ) as ?to )

  bind( iri( concat( func:getNamespace( ?aspect ), ?aspectBoxName, "_To_", ?operationBoxName ) ) as ?edge )
}
