# Copyright (c) 2023 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.1.0#>
prefix samm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:2.1.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.1.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates boxes for Units
construct {
  ?unitBox a :Box .
  ?unitBox :prototype "Unit" .
  ?unitBox :title ?upperUnitName .
  ?unitBox :modelElement ?unit .
  ?unitBox :entries (
     [
       a :Entry ;
       :title "symbol" ;
       :text ?symbol
     ]
  )
} where {
  ?measurement samm-c:unit ?unit .
  # Select only those Units that are used in the Aspect
  filter( exists { ?property samm:characteristic/( samm-c:baseCharacteristic* | rdfs:subClassOf* ) ?measurement } )
  bind( strafter( str( ?unit ), "#" ) as ?unitName ) .
  bind( concat( ucase( substr( ?unitName, 1, 1 ) ), substr( ?unitName, 2 ) ) as ?upperUnitName )

  bind( concat( ?upperUnitName, "Unit" ) as ?boxName )
  bind( iri( concat( func:getNamespace( ?unit ), ?boxName ) ) as ?unitBox )

  optional {
    ?unit samm:symbol ?symbol
  }
}
