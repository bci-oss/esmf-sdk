# Copyright (c) 2023 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.1.0#>
prefix samm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:2.1.0#>
prefix samm-e: <urn:samm:org.eclipse.esmf.samm:entity:2.1.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.1.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates boxes for Entity instances
construct {
  ?entityBox a :Box .
  ?entityBox :prototype ?prototype .
  ?entityBox :title ?instanceName .
  ?entityBox :modelElement ?instance .
  ?entityBox :entry [
    a :Entry ;
    :title ?entryTitle ;
    :text ?entryText
  ] .
} where {
  ?instance a ?entity .
  ?entity a samm:Entity .

  filter( exists {
    ?aspect a samm:Aspect .
    ?aspect (<>|!<>)* ?instance .
  } && exists {
    ?aspect a samm:Aspect .
    ?aspect (<>|!<>)* ?entity .
  } )

  bind( strafter( str( ?entity ), "#" ) as ?prototype ) .
  bind( strafter( str( ?instance ), "#" ) as ?instanceName )

  bind( concat( ?instanceName, "EntityInstance" ) as ?boxName )
  bind( iri( concat( func:getNamespace( ?instance ), ?boxName ) ) as ?entityBox )

  ?entity samm:properties/rdf:rest*/rdf:first*/samm:property* ?property .
  ?property samm:characteristic/samm-c:baseCharacteristc*/samm:dataType ?dataType .
  filter ( not exists { ?dataType rdf:type/rdfs:subclassOf* samm:Entity } ) .

  bind( strafter( str( ?property ), "#" ) as ?propertyName ) .
  bind( ?propertyName as ?entryTitle )

  ?instance ?property ?propertyValue .
  bind( str( ?propertyValue ) as ?value )

  optional {
    select ?property ( group_concat( distinct ?listValue;separator=";" ) as ?value ) {
      ?instance ?property ?propertyValue
      filter ( isBlank(?propertyValue) )
      ?propertyValue rdf:rest*/rdf:first ?listValue
    }
    group by ?property
  }

  bind( str( ?value ) as ?entryText )
}
