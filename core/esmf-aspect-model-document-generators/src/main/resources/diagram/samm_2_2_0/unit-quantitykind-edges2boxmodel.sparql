# Copyright (c) 2023 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.1.0#>
prefix samm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:2.1.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.1.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates edges between Units and QuantityKinds (quantityKind)
construct {
  ?edge a :Edge .
  ?edge :title "quantityKind" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  [] samm-c:unit ?unit .
  ?unit samm:referenceUnit?/samm:quantityKind ?quantityKind .
  bind( strafter( str( ?unit ), "#" ) as ?unitName ) .
  bind( concat( ucase( substr( ?unitName, 1, 1 ) ), substr( ?unitName, 2 ) ) as ?upperUnitName )
  bind( strafter( str( ?quantityKind ), "#" ) as ?quantityKindGivenName ) .
  bind( concat( ?quantityKindGivenName, "QuantityKind" ) as ?quantityKindName )
  bind( concat( ucase( substr( ?quantityKindName, 1, 1 ) ), substr( ?quantityKindName, 2 ) ) as ?upperQuantityKindName )

  bind( concat( ?upperUnitName, "Unit" ) as ?unitBoxName )
  bind( iri( concat( func:getNamespace( ?unit ), ?unitBoxName ) ) as ?from )
  bind( replace( ?upperQuantityKindName, "-", "" ) as ?quantityKindBoxName )
  bind( iri( concat( func:getNamespace( ?quantityKind ), ?quantityKindBoxName ) ) as ?to )

  bind( iri( concat( func:getNamespace( ?unit ), ?unitBoxName, "_To_", ?quantityKindBoxName ) ) as ?edge )
}

