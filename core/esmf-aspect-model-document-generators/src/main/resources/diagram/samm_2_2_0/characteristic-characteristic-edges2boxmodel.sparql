# Copyright (c) 2023 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.1.0#>
prefix samm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:2.1.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.1.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates edges between Trait and other Characteristics (baseCharacteristic)
construct {
  ?edge a :Edge .
  ?edge :title "baseCharacteristic" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  bind( func:getElementName( ?characteristic ) as ?characteristicName )
  ?characteristic samm-c:baseCharacteristic ?baseCharacteristic .
  bind( func:getElementName( ?baseCharacteristic ) as ?baseCharacteristicName )
  bind( concat( ?characteristicName, "Characteristic" ) as ?box1Name )
  bind( iri( concat( func:getNamespace( ?characteristic ), ?box1Name ) ) as ?from )

  bind( concat( ?baseCharacteristicName, "Characteristic" ) as ?box2Name )
  bind( iri( concat( func:getNamespace( ?baseCharacteristic ), ?box2Name ) ) as ?to )

  bind( iri( concat( func:getNamespace( ?characteristic ), ?box1Name, "_To_", ?box2Name ) ) as ?edge )
}

