# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:1.0.0#>
prefix samm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:1.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:1.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates edges between StructuredValues its elements Properties
construct {
  ?edge a :Edge .
  ?edge :title ?propertyName .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?characteristic rdf:type/rdfs:subClassOf* samm-c:StructuredValue .
  ?characteristic samm:name ?characteristicName .

  bind( concat( ?characteristicName, "Characteristic" ) as ?characteristicBoxName )
  bind( iri( concat( func:getNamespace( ?characteristic ), ?characteristicBoxName ) ) as ?from )

  ?characteristic samm-c:elements/rdf:rest*/rdf:first ?element .
  filter( exists { ?element a samm:Property } )
  ?element samm:name ?propertyName .
  bind( concat( ?propertyName, "Property" ) as ?propertyBoxName )
  bind( iri( concat( func:getNamespace( ?element ), ?propertyBoxName ) ) as ?to )

  bind( iri( concat( func:getNamespace( ?characteristic ), ?characteristicBoxName, "_To_", ?propertyBoxName ) ) as ?edge )
}

