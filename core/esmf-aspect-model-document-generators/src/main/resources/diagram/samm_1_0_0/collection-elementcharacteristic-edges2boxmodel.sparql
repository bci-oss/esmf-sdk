# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:1.0.0#>
prefix samm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:1.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:1.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates edges between Collections and Element Characteristics
construct {
  ?edge a :Edge .
  ?edge :title "element Characteristic" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?characteristic samm:name ?characteristicName .
  ?characteristic samm-c:elementCharacteristic ?elementCharacteristic .
  ?elementCharacteristic samm:name ?elementCharacteristicName .
  bind( concat( ?characteristicName, "Characteristic" ) as ?box1Name )
  bind( iri( concat( func:getNamespace( ?characteristic ), ?box1Name ) ) as ?from )

  bind( concat( ?elementCharacteristicName, "Characteristic" ) as ?box2Name )
  bind( iri( concat( func:getNamespace( ?elementCharacteristic ), ?box2Name ) ) as ?to )

  bind( iri( concat( func:getNamespace( ?characteristic ), ?box1Name, "_To_", ?box2Name ) ) as ?edge )
}

