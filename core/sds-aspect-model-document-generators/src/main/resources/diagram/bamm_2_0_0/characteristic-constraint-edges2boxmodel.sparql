# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:bamm:io.openmanufacturing:meta-model:2.0.0#>
prefix bamm-c: <urn:bamm:io.openmanufacturing:characteristic:2.0.0#>
prefix bamm-e: <urn:bamm:io.openmanufacturing:entity:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:bamm:io.openmanufacturing:meta-model:2.0.0/boxmodel#>
prefix func: <urn:bamm:io.openmanufacturing:function:2.0.0#>

# Generates edges between Trait and Constraints (constraint)
construct {
  ?edge a :Edge .
  ?edge :title "constraint" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?characteristic bamm-c:constraint ?constraint .
  bind( func:getElementName( ?characteristic ) as ?characteristicName )

  bind( concat( ?characteristicName, "Characteristic" ) as ?characteristicBoxName )
  bind( iri( concat( str( : ), ?characteristicBoxName ) ) as ?from )

  bind( func:getElementName( ?constraint ) as ?constraintName )
  bind( concat( ?constraintName, "Constraint" ) as ?constraintBoxName )

  bind( iri( concat( str( : ), ?constraintBoxName ) ) as ?to )

  bind( iri( concat( str( : ), ?characteristicBoxName, "_To_", ?constraintBoxName ) ) as ?edge )
}

