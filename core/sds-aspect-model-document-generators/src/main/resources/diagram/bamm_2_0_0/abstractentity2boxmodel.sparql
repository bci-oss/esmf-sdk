# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:bamm:io.openmanufacturing:meta-model:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:bamm:io.openmanufacturing:meta-model:2.0.0/boxmodel#>

# Generates boxes for Abstract Entities
construct {
  ?abstractEntityBox a :Box .
  ?abstractEntityBox :prototype "Abstract Entity" .
  ?abstractEntityBox :title ?abstractEntityName .
  ?abstractEntityBox :modelElement ?abstractEntity .
  ?abstractEntityBox :entries (
     [
       a :Entry ;
       :title "preferredName" ;
       :text ?preferredName
     ]
     [
       a :Entry ;
       :title "description" ;
       :text ?description
     ]
     [
       a :Entry ;
       :title "see" ;
       :text ?seeValues
     ]
  )
} where {
  ?abstractEntity rdf:type bamm:AbstractEntity .
  bind( strafter( str( ?abstractEntity ), "#" ) as ?abstractEntityName )

  bind( concat( ?abstractEntityName, "AbstractEntity" ) as ?boxName )
  bind( iri( concat( str( : ), ?boxName ) ) as ?abstractEntityBox )

  optional {
    ?abstractEntity bamm:preferredName ?preferredNameValue .
    filter( lang( ?preferredNameValue ) = "en" )
    bind( xsd:string( ?preferredNameValue ) as ?preferredName )
  }

  optional {
    ?abstractEntity bamm:description ?descriptionValue .
    filter( lang( ?descriptionValue ) = "en" )
    bind( xsd:string( ?descriptionValue ) as ?description )
  }

  optional {
    {
      select ( group_concat( ?seeValue; separator=", " ) as ?seeValues ) ?abstractEntity
      where {
        select *
        where {
          ?abstractEntity rdf:type bamm:AbstractEntity .
          ?abstractEntity bamm:see ?seeValue .
        }
        order by str( ?seeValue )
      }
      group by ?abstractEntity
    }
  }
}
